layout(quads, fractional_even_spacing, cw) in;

out vec2 teTexCoords;

uniform sampler2D displacementMap;

uniform vec2 nodeLocation;
uniform float nodeSideLength;

uniform float displacementScale;
uniform float displacementDistance;

uniform mat4 vMatrix;
uniform mat4 pMatrix;

uniform vec3 cameraLocation;


void main(){

    float u = gl_TessCoord.x;
    float v = gl_TessCoord.y;
	
	//  vec3 pos = gl_in[0].gl_Position.xyz;
    // pos.xz += gl_TessCoord.xy * tileSize.xz;
	// Use barrycentric coordinates to carefully calculate the height
	
	// world position
	vec4 position =
	((1.0f - u) * (1.0f - v) * gl_in[3].gl_Position +
	u * (1.0f - v) * gl_in[0].gl_Position +
	u * v * gl_in[1].gl_Position +
	(1.0f - u) * v * gl_in[2].gl_Position);
	
	float vertexDistance = distance(position.xyz, cameraLocation);
	
	vec2 texCoords = vec2(position.x, position.z) - nodeLocation;
	texCoords /= nodeSideLength;
	
	teTexCoords = texCoords;
	
	float displacement = texture(displacementMap, texCoords * 40.0f).r * displacementScale * clamp((displacementDistance - vertexDistance), 0.0f, 1.0f);
	
	position.y += displacement;
	
#ifndef GEOMETRY_SHADER
	position = pMatrix * vMatrix * position;
#endif
	
	gl_Position = position;
	
}